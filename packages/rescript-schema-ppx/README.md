[‚¨Ö Back to highlights](/README.md)

# ReScript Schema PPX

ReScript PPX to generate **rescript-schema** from type.

> üß† It's 100% opt-in. You can use **rescript-schema** without ppx.

## Table of contents

- [Table of contents](#table-of-contents)
- [Install](#install)
- [Basic usage](#basic-usage)
- [API reference](#api-reference)

## Install

```sh
npm install rescript-schema rescript-schema-ppx
```

Then update your `rescript.json` config:

```diff
{
  ...
+ "bs-dependencies": ["rescript-schema"],
+ "bsc-flags": ["-open RescriptSchema"],
+ "ppx-flags": ["rescript-schema-ppx/bin"],
}
```

## Basic usage

```rescript
// 1. Define a type and add @schema attribute
@schema
type rating =
  | @as("G") GeneralAudiences
  | @as("PG") ParentalGuidanceSuggested
  | @as("PG13") ParentalStronglyCautioned
  | @as("R") Restricted
@schema
type film = {
  @as("Id")
  id: float,
  @as("Title")
  title: string,
  @as("Tags")
  tags: @s.default([]) array<string>,
  @as("Rating")
  rating: rating,
  @as("Age")
  deprecatedAgeRestriction: @s.deprecate("Use rating instead") option<int>,
}

// 2. Generated by PPX ‚¨áÔ∏è
let ratingSchema = S.union([
  S.literal(GeneralAudiences),
  S.literal(ParentalGuidanceSuggested),
  S.literal(ParentalStronglyCautioned),
  S.literal(Restricted),
])
let filmSchema = S.object(s => {
  id: s.field("Id", S.float),
  title: s.field("Title", S.string),
  tags: s.fieldOr("Tags", S.array(S.string), []),
  rating: s.field("Rating", ratingSchema),
  deprecatedAgeRestriction: s.field("Age", S.option(S.int)->S.deprecate("Use rating instead")),
})

// 3. Parse data using the schema
// The data is validated and transformed to a convenient format
%raw(`{
  "Id": 1,
  "Title": "My first film",
  "Rating": "R",
  "Age": 17
}`)->S.parseOrThrow(filmSchema)
// Ok({
//   id: 1.,
//   title: "My first film",
//   tags: [],
//   rating: Restricted,
//   deprecatedAgeRestriction: Some(17),
// })

// 4. Transform data back using the same schema
{
  id: 2.,
  tags: ["Loved"],
  title: "Sad & sed",
  rating: ParentalStronglyCautioned,
  deprecatedAgeRestriction: None,
}->S.reverseConvertOrThrow(filmSchema)
// Ok(%raw(`{
//   "Id": 2,
//   "Title": "Sad & sed",
//   "Rating": "PG13",
//   "Tags": ["Loved"],
//   "Age": undefined,
// }`))

// 5. Use schema as a building block for other tools
// For example, create a JSON-schema with rescript-json-schema and use it for OpenAPI generation
let filmJSONSchema = JSONSchema.make(filmSchema)
```

> üß† Read more about schema usage in the _[ReScript Schema for ReScript users](/docs/rescript-usage.md)_ documentation.

## API reference

### `@schema`

**Applies to**: type declarations, type signatures

Indicates that a schema should be generated for the given type.

### `@s.matches(S.t<'value>)`

**Applies to**: type expressions

Specifies custom schema for the type.

```rescript
@schema
type t = @s.matches(S.string->S.url) string

// Generated by PPX ‚¨áÔ∏è
let schema = S.string->S.url
```

### `@s.null`

**Applies to**: option type expressions

Tells to use `S.null` for the option schema constructor.

```rescript
@schema
type t = @s.null option<string>

// Generated by PPX ‚¨áÔ∏è
let schema = S.null(S.string)
```

### `@s.nullable`

**Applies to**: option type expressions

Tells to use `S.nullable` for the option schema constructor.

```rescript
@schema
type t = @s.nullable option<string>

// Generated by PPX ‚¨áÔ∏è
let schema = S.nullable(S.string)
```

### `@s.default('value)`

**Applies to**: type expressions

Wraps the type expression schema into an option with the provided default value.

```rescript
@schema
type t = @s.default("Unknown") string

// Generated by PPX ‚¨áÔ∏è
let schema = S.option(S.string)->S.Option.getOr("Unknown")
```

It might be used together with `@s.null` or `@s.nullable`:

```rescript
@schema
type t = @s.nullable @s.default("Unknown") string

// Generated by PPX ‚¨áÔ∏è
let schema = S.nullable(S.string)->S.Option.getOr("Unknown")
```

### `@s.defaultWith(unit => 'value)`

**Applies to**: type expressions

Wraps the type expression schema into an option with callback to get the default value.

```rescript
@schema
type t = @s.defaultWith(() => []) array<string>

// Generated by PPX ‚¨áÔ∏è
let schema = S.option(S.array(S.string))->S.Option.getOrWith(() => [])
```

> üß† The same as `@s.default` it might be used together with `@s.null` or `@s.nullable`

### `@s.describe(string)`

**Applies to**: type expressions

Adds `description` property to the generated schema.

```rescript
@schema
type t = @s.describe("A useful bit of text, if you know what to do with it.") string

// Generated by PPX ‚¨áÔ∏è
let schema = S.string->S.describe("A useful bit of text, if you know what to do with it.")
```

This can be useful for documenting a field, for example in a JSON Schema using a library like [`rescript-json-schema`](https://github.com/DZakh/rescript-json-schema).

### `@s.deprecate(string)`

**Applies to**: type expressions

Adds `deprecation` property to the generated schema.

```rescript
@schema
type t = @s.deprecate("Will be removed in APIv2") string

// Generated by PPX ‚¨áÔ∏è
let schema = S.string->S.deprecate("Will be removed in APIv2")
```

This can be useful for documenting a field, for example in a JSON Schema using a library like [`rescript-json-schema`](https://github.com/DZakh/rescript-json-schema).
