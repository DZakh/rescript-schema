open Vitest

module Common = {
  let value = None
  let any = %raw(`undefined`)
  let invalidAny = %raw(`123.45`)
  let factory = () => S.option(S.string)

  test("Successfully parses", t => {
    let schema = factory()

    t->Assert.deepEqual(any->S.parseOrThrow(~to=schema), value)
  })

  test("Fails to parse", t => {
    let schema = factory()

    t->U.assertThrowsMessage(
      () => invalidAny->S.parseOrThrow(~to=schema),
      `Expected string | undefined, received 123.45`,
    )
  })

  test("Successfully serializes", t => {
    let schema = factory()

    t->Assert.deepEqual(value->S.convertOrThrow(~from=schema, ~to=S.unknown), any)
  })

  test("Compiled parse code snapshot", t => {
    let schema = factory()

    t->U.assertCompiledCode(
      ~schema,
      ~op=#Parse,
      `i=>{(typeof i==="string"||i===void 0)||e[0](i);return i}`,
    )
  })

  // Undefined check should be first ?
  test("Compiled async parse code snapshot", t => {
    let schema = S.option(
      S.unknown->S.to(S.any, ~custom={decode: Async(i => Promise.resolve(i)), encode: Never}),
    )

    t->U.assertCompiledCode(
      ~schema,
      ~op=#ParseAsync,
      `i=>{try{return Promise.resolve((async(i)=>{for(;;){let r;try{let v0=e[0](i);i=await v0;break}catch(x){(r||(r=[])).push(e[1](x))}if(i===void 0)break;e[2](i,...(r||[]))};return i})(i))}catch(v1){return Promise.reject(v1)}}`,
    )
  })

  test("Compiled serialize code snapshot", t => {
    let schema = factory()

    t->U.assertCompiledCode(
      ~schema,
      ~op=#Encode,
      `i=>{(typeof i==="string"||i===void 0)||e[0](i);return i}`,
    )
  })

  test("Reverse to self", t => {
    let schema = factory()
    t->U.assertEqualSchemas(schema->S.reverse, schema->S.castToUnknown)
  })

  test("Succesfully uses reversed schema for parsing back to initial value", t => {
    let schema = factory()
    t->U.assertReverseParsesBack(schema, Some("abc"))
    t->U.assertReverseParsesBack(schema, None)
  })
}

test("Classify schema", t => {
  let schema = S.option(S.nullAsOption(S.string))

  t->U.assertEqualSchemas(
    schema->S.castToUnknown,
    S.union([
      S.string->S.castToUnknown,
      S.unit->S.castToUnknown,
      S.nullAsUnit->S.to(S.literal({"BS_PRIVATE_NESTED_SOME_NONE": 0}))->S.castToUnknown,
    ]),
  )

  t->U.assertEqualSchemas(
    schema->S.reverse,
    S.union([
      S.string->S.castToUnknown,
      S.unit->S.castToUnknown,
      S.literal({"BS_PRIVATE_NESTED_SOME_NONE": 0})->S.to(S.nullAsUnit->S.reverse)->S.castToUnknown,
    ]),
  )
})

test("Successfully parses primitive", t => {
  let schema = S.option(S.bool)

  t->Assert.deepEqual(JSON.Encode.bool(true)->S.parseOrThrow(~to=schema), Some(true))
})

test("Fails to parse JS null", t => {
  let schema = S.option(S.bool)

  t->U.assertThrowsMessage(
    () => %raw(`null`)->S.parseOrThrow(~to=schema),
    `Expected boolean | undefined, received null`,
  )
})

test("Fails to parse JS undefined when schema doesn't allow optional data", t => {
  let schema = S.bool

  t->U.assertThrowsMessage(
    () => %raw(`undefined`)->S.parseOrThrow(~to=schema),
    `Expected boolean, received undefined`,
  )
})

test("Serializes Some(None) to undefined for option nested in null", t => {
  let schema = S.nullAsOption(S.option(S.bool))

  t->Assert.deepEqual(%raw(`undefined`)->S.parseOrThrow(~to=schema), Some(None))
  t->Assert.deepEqual(%raw(`null`)->S.parseOrThrow(~to=schema), None)

  t->Assert.deepEqual(Some(None)->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`null`))

  t->U.assertCompiledCode(
    ~schema,
    ~op=#Parse,
    `i=>{for(;;){if(typeof i==="boolean")break;if(i===null){i=void 0;break}if(i===void 0){i={BS_PRIVATE_NESTED_SOME_NONE:0};break}e[0](i)}return i}`,
  )
  t->U.assertCompiledCode(
    ~schema,
    ~op=#Encode,
    `i=>{for(;;){if(typeof i==="boolean")break;if(i===void 0){i=null;break}if(typeof i==="object"&&i&&!Array.isArray(i)&&i.BS_PRIVATE_NESTED_SOME_NONE===0){i=void 0;break}e[0](i)}return i}`,
  )
})

test("Applies valFromOption for Some()", t => {
  let schema = S.option(S.literal())

  t->Assert.deepEqual(%raw(`undefined`)->S.parseOrThrow(~to=schema), None)
  t->Assert.deepEqual(Some()->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCode(~schema, ~op=#Parse, `i=>{i===void 0||e[0](i);return i}`)
  t->U.assertCompiledCode(
    ~schema,
    ~op=#Encode,
    `i=>{for(;;){if(i===void 0)break;if(typeof i==="object"&&i&&!Array.isArray(i)&&i.BS_PRIVATE_NESTED_SOME_NONE===0){i=void 0;break}e[0](i)}return i}`,
  )
})

test("Nested option support", t => {
  let schema = S.option(S.option(S.bool))

  t->Assert.deepEqual(%raw(`undefined`)->S.parseOrThrow(~to=schema), None)
  t->Assert.deepEqual(Some(Some(true))->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`true`))
  t->Assert.deepEqual(Some(None)->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCode(
    ~schema,
    ~op=#Parse,
    `i=>{(typeof i==="boolean"||i===void 0)||e[0](i);return i}`,
  )
  t->U.assertCompiledCode(
    ~schema,
    ~op=#Encode,
    `i=>{for(;;){if(typeof i==="boolean")break;if(i===void 0)break;if(typeof i==="object"&&i&&!Array.isArray(i)&&i.BS_PRIVATE_NESTED_SOME_NONE===0){i=void 0;break}e[0](i)}return i}`,
  )
})

test("Triple nested option support", t => {
  let schema = S.option(S.option(S.option(S.bool)))

  t->Assert.deepEqual(%raw(`undefined`)->S.parseOrThrow(~to=schema), None)
  t->Assert.deepEqual(
    Some(Some(Some(true)))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`true`),
  )
  t->Assert.deepEqual(
    Some(Some(None))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`undefined`),
  )
  t->Assert.deepEqual(Some(None)->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCode(
    ~schema,
    ~op=#Parse,
    `i=>{(typeof i==="boolean"||i===void 0)||e[0](i);return i}`,
  )
  t->U.assertCompiledCode(
    ~schema,
    ~op=#Encode,
    `i=>{for(;;){if(typeof i==="boolean")break;if(i===void 0)break;if(typeof i==="object"&&i&&!Array.isArray(i)){for(;;){if(i.BS_PRIVATE_NESTED_SOME_NONE===0){i=void 0;break}if(i.BS_PRIVATE_NESTED_SOME_NONE===1){i=void 0;break}e[0](i)};break}e[1](i)}return i}`,
  )
})

test(
  "Empty object in option: S.option(S.object(_ => ())) https://github.com/DZakh/rescript-schema/issues/110",
  t => {
    let schema = S.option(S.object(_ => ()))

    t->Assert.deepEqual(%raw(`undefined`)->S.parseOrThrow(~to=schema), None)
    t->Assert.deepEqual(%raw(`{}`)->S.parseOrThrow(~to=schema), Some())
    t->Assert.deepEqual(Some()->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`{}`))
    t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

    t->U.assertCompiledCode(
      ~schema,
      ~op=#Parse,
      `i=>{for(;;){if(i===void 0)break;if(typeof i==="object"&&i&&!Array.isArray(i)){i={BS_PRIVATE_NESTED_SOME_NONE:0};break}e[0](i)}return i}`,
    )
    t->U.assertCompiledCode(
      ~schema,
      ~op=#Encode,
      `i=>{for(;;){if(i===void 0)break;if(typeof i==="object"&&i&&!Array.isArray(i)&&i.BS_PRIVATE_NESTED_SOME_NONE===0){i={};break}e[0](i)}return i}`,
    )
  },
)

test("Doesn't apply valFromOption for non-undefined literals in option", t => {
  let schema: S.t<option<Null.t<unknown>>> = S.option(S.literal(%raw(`null`)))

  // Note: It'll fail without a type annotation, but we can't do anything here
  t->Assert.deepEqual(
    Some(%raw(`null`))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`null`),
  )
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCode(~schema, ~op=#Encode, `i=>{(i===null||i===void 0)||e[0](i);return i}`)
})

test("Option with unknown", t => {
  let schema = S.option(S.unknown)

  t->Assert.deepEqual(
    Some(%raw(`undefined`))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`{BS_PRIVATE_NESTED_SOME_NONE: 0}`),
  )
  t->Assert.deepEqual(
    Some(%raw(`"foo"`))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`"foo"`),
  )
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCodeIsNoop(~schema, ~op=#Parse)
  t->U.assertCompiledCodeIsNoop(~schema, ~op=#Encode)
})

test("Option with transformed unknown", t => {
  let schema = S.option(S.unknown->S.shape(v => {"field": v}))

  t->Assert.deepEqual(
    Some(%raw(`undefined`))->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`undefined`),
  )
  t->Assert.deepEqual(
    Some({"field": %raw(`"foo"`)})->S.convertOrThrow(~from=schema, ~to=S.unknown),
    %raw(`"foo"`),
  )
  t->Assert.deepEqual(None->S.convertOrThrow(~from=schema, ~to=S.unknown), %raw(`undefined`))

  t->U.assertCompiledCode(~schema, ~op=#Parse, `i=>{for(;;){i={field:i};break;}return i}`)
  t->U.assertCompiledCode(
    ~schema,
    ~op=#Encode,
    `i=>{for(;;){if(typeof i==="object"&&i&&!Array.isArray(i)){i=i.field;break}if(i===void 0)break;e[0](i)}return i}`,
  )
})
