@@uncurried

%%private(external magic: 'a => 'b = "%identity")

module Arrayable = {
  type t<'item>
  type tagged<'item> = Single('item) | Array(array<'item>)

  external array: array<'item> => t<'item> = "%identity"
  external single: 'item => t<'item> = "%identity"

  external isArray: t<'item> => bool = "Array.isArray"

  let classify = (arrayable: t<'item>): tagged<'item> => {
    if arrayable->isArray {
      Array(arrayable->(magic: t<'item> => array<'item>))
    } else {
      Single(arrayable->(magic: t<'item> => 'item))
    }
  }
}

/**
 * Primitive type
 * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.1.1
 */
type typeName = [
  | #string
  | #number
  | #integer
  | #boolean
  | #object
  | #array
  | #null
]

/**
 * Meta schema
 *
 * Recommended values:
 * - 'http://json-schema.org/schema#'
 * - 'http://json-schema.org/hyper-schema#'
 * - 'http://json-schema.org/draft-07/schema#'
 * - 'http://json-schema.org/draft-07/hyper-schema#'
 *
 * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-5
 */
type version = string

/**
 * A JSON Schema of any dialect Sury understands - draft-06 through
 * draft-2020-12, plus the OpenAPI 3.0 `nullable` extension (OpenAPI.res aliases
 * this as its schema object).
 *
 * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01
 */
type rec t = {
  @as("$id")
  id?: string,
  @as("$ref")
  ref?: string,
  @as("$schema")
  schema?: version,
  /**
   * @see https://datatracker.ietf.org/doc/html/draft-bhutton-json-schema-00#section-8.2.4
   * @see https://datatracker.ietf.org/doc/html/draft-bhutton-json-schema-validation-00#appendix-A
   */
  @as("$defs")
  defs?: dict<definition>,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.1
   */
  @as("type")
  type_?: Arrayable.t<typeName>,
  enum?: array<JSON.t>,
  const?: JSON.t,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.2
   */
  multipleOf?: float,
  maximum?: float,
  exclusiveMaximum?: float,
  minimum?: float,
  exclusiveMinimum?: float,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.3
   */
  maxLength?: int,
  minLength?: int,
  pattern?: string,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.4
   */
  items?: Arrayable.t<definition>,
  prefixItems?: array<definition>,
  additionalItems?: definition,
  unevaluatedItems?: definition,
  maxItems?: int,
  minItems?: int,
  uniqueItems?: bool,
  contains?: definition,
  minContains?: int,
  maxContains?: int,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.5
   */
  maxProperties?: int,
  minProperties?: int,
  required?: array<string>,
  properties?: dict<definition>,
  patternProperties?: dict<definition>,
  additionalProperties?: definition,
  unevaluatedProperties?: definition,
  dependencies?: dict<dependency>,
  dependentSchemas?: dict<definition>,
  dependentRequired?: dict<array<string>>,
  propertyNames?: definition,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.6
   */
  @as("if")
  if_?: definition,
  then?: definition,
  @as("else")
  else_?: definition,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.7
   */
  allOf?: array<definition>,
  anyOf?: array<definition>,
  oneOf?: array<definition>,
  not?: definition,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-7
   */
  format?: string,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-8
   */
  contentMediaType?: string,
  contentEncoding?: string,
  contentSchema?: definition,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-9
   */
  definitions?: dict<definition>,
  /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-10
   */
  title?: string,
  description?: string,
  deprecated?: bool,
  nullable?: bool,
  default?: JSON.t,
  readOnly?: bool,
  writeOnly?: bool,
  examples?: array<JSON.t>,
}
@unboxed and definition = Schema(t) | @as(false) Never | @as(true) Any
@unboxed and dependency = RequiredSchema(t) | RequiredProperties(array<string>)

module Mutable = {
  type readOnly = t
  type t = {
    @as("$id")
    mutable id?: string,
    @as("$ref")
    mutable ref?: string,
    @as("$schema")
    mutable schema?: version,
    /**
   * @see https://datatracker.ietf.org/doc/html/draft-bhutton-json-schema-00#section-8.2.4
   * @see https://datatracker.ietf.org/doc/html/draft-bhutton-json-schema-validation-00#appendix-A
   */
    @as("$defs")
    mutable defs?: dict<definition>,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.1
   */
    @as("type")
    mutable type_?: Arrayable.t<typeName>,
    mutable enum?: array<JSON.t>,
    mutable const?: JSON.t,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.2
   */
    mutable multipleOf?: float,
    mutable maximum?: float,
    mutable exclusiveMaximum?: float,
    mutable minimum?: float,
    mutable exclusiveMinimum?: float,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.3
   */
    mutable maxLength?: int,
    mutable minLength?: int,
    mutable pattern?: string,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.4
   */
    mutable items?: Arrayable.t<definition>,
    mutable prefixItems?: array<definition>,
    mutable additionalItems?: definition,
    mutable unevaluatedItems?: definition,
    mutable maxItems?: int,
    mutable minItems?: int,
    mutable uniqueItems?: bool,
    mutable contains?: definition,
    mutable minContains?: int,
    mutable maxContains?: int,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.5
   */
    mutable maxProperties?: int,
    mutable minProperties?: int,
    mutable required?: array<string>,
    mutable properties?: dict<definition>,
    mutable patternProperties?: dict<definition>,
    mutable additionalProperties?: definition,
    mutable unevaluatedProperties?: definition,
    mutable dependencies?: dict<dependency>,
    mutable dependentSchemas?: dict<definition>,
    mutable dependentRequired?: dict<array<string>>,
    mutable propertyNames?: definition,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.6
   */
    @as("if")
    mutable if_?: definition,
    mutable then?: definition,
    @as("else")
    mutable else_?: definition,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-6.7
   */
    mutable allOf?: array<definition>,
    mutable anyOf?: array<definition>,
    mutable oneOf?: array<definition>,
    mutable not?: definition,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-7
   */
    mutable format?: string,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-8
   */
    mutable contentMediaType?: string,
    mutable contentEncoding?: string,
    mutable contentSchema?: definition,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-9
   */
    mutable definitions?: dict<definition>,
    /**
   * @see https://tools.ietf.org/html/draft-handrews-json-schema-validation-01#section-10
   */
    mutable title?: string,
    mutable description?: string,
    mutable deprecated?: bool,
    mutable nullable?: bool,
    mutable default?: JSON.t,
    mutable readOnly?: bool,
    mutable writeOnly?: bool,
    mutable examples?: array<JSON.t>,
  }
  @unboxed and definition = Schema(t) | @as(false) Never | @as(true) Any
  @unboxed and dependency = RequiredSchema(t) | RequiredProperties(array<string>)

  external fromReadOnly: readOnly => t = "%identity"
  external toReadOnly: t => readOnly = "%identity"

  @val
  external mixin: (t, readOnly) => unit = "Object.assign"
}
